import java.nio.file.*
import java.nio.file.attribute.BasicFileAttributes

class DeletingFileVisitor extends SimpleFileVisitor<Path> {
	@Override
	FileVisitResult visitFile(Path path, BasicFileAttributes basicFileAttributes) throws IOException {
		Files.delete(path);
		return FileVisitResult.CONTINUE;
	}

	@Override
	FileVisitResult postVisitDirectory(Path directory, IOException ioException) throws IOException {
		Files.delete(directory);
		return FileVisitResult.CONTINUE;
	}
}

plugins {
	id 'com.gradleup.shadow'
}

architectury {
	platformSetupLoomIde()
	forge()
}

version = rootProject.mod_version + "-forge-" + rootProject.minecraft_version

loom {
	forge {
		mixinConfig "taxfreelevels-common-forge.mixins.json"
		mixinConfig "taxfreelevels-forge.mixins.json"
	}
}

configurations {
	common {
		canBeResolved = true
		canBeConsumed = false
	}
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentForge.extendsFrom common

	// Files in this configuration will be bundled into your mod using the Shadow plugin.
	// Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
	shadowBundle {
		canBeResolved = true
		canBeConsumed = false
	}
}

dependencies {
	forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

	compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${project.mixinextras_version}"))
	implementation(include("io.github.llamalad7:mixinextras-forge:${project.mixinextras_version}"))

	modApi "me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}"

	modCompileOnly "maven.modrinth:enchanting-infuser:${project.enchanting_infuser_version}-Forge"

	common(project(path: ':common', configuration: 'namedElements')) { transitive false }
	shadowBundle project(path: ':common', configuration: 'transformProductionForge')
}

shadowJar {
	configurations = [project.configurations.shadowBundle]
	archiveClassifier = 'dev-shadow'
}

remapJar {
	input.set shadowJar.archiveFile
}

task finalize {
	doLast {
		// delete "header files" from jar
		var jar = remapJar.archiveFile.get().asFile.toPath()
		if (Files.exists(jar)) {
			var jarFileUri = new URI('jar:file', jar.toUri().getSchemeSpecificPart(), jar.toUri().getFragment())

			def del = (fs, pkg) -> {
				def path = fs.getPath(pkg)
				if (Files.exists(path)) Files.walkFileTree(path, new DeletingFileVisitor());
			}

			try (FileSystem fs = FileSystems.newFileSystem(jarFileUri, ['create': 'false'])) {
				del(fs, "net")
				del(fs, "dev")
			}
		}
	}
}

build.finalizedBy(finalize)

sourcesJar {
	def commonSources = project(":common").sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}


publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = rootProject.archives_base_name + "-" + project.name
			from components.java
		}
	}
}
